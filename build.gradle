plugins {
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
	alias(libs.plugins.quilt.loom)
	alias(libs.plugins.loom.quiltflower)
	alias(libs.plugins.mappings.loom)
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + "build." + project.build_number + "-" + libs.versions.minecraft.get()
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/vampirelib.accesswidener")

	runs {
		datagenClient {
			client()
			name "Data Generation Client"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
			vmArg "-Dfabric-api.datagen.existing-dir=${file("src/main/resources")}"
			vmArg "-Dfabric-api.datagen.strict_validation=false"

			ideConfigGenerated = true
			runDir "build/datagen"
		}
	}
}

// Include resources generated by data generators.
sourceSets {
	main {
		resources {
			srcDirs += [
				'src/main/generated'
			]
		}
	}
}

repositories {
	mavenCentral()
	mavenLocal()
	maven { url = "https://maven.fabricmc.net" }
	maven { url = "https://maven.shedaniel.me/" }
	maven { url = "https://storage.googleapis.com/devan-maven/" }
	maven { url = "https://maven.parchmentmc.net/" }
	maven { url = "https://maven.terraformersmc.com/" }
	maven { url = "https://maven.siphalor.de" }
}

dependencies {
	minecraft libs.minecraft
	mappings loom.layered {
		it.addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:v2"))
		it.parchment("org.parchmentmc.data:parchment-1.18.2:2022.03.13@zip")
		it.officialMojangMappings {
			setNameSyntheticMembers(false)
		}
	}

	modImplementation libs.quilt.loader

	modImplementation (libs.fabric.api) {
		force = true
		exclude(group: "net.fabricmc", module: "fabric-loader")
	}

	modApi (libs.clothconfig) {
		exclude(group: "net.fabricmc.fabric-api")
		exclude(group: "net.fabricmc", module: "fabric-loader")
	}
	include (libs.clothconfig) {
		exclude(group: "net.fabricmc.fabric-api")
		exclude(group: "net.fabricmc", module: "fabric-loader")
	}

	modApi (libs.artifice) {
		exclude(group: "net.fabricmc.fabric-api")
		exclude(group: "net.fabricmc", module: "fabric-loader")
	}
	include (libs.artifice) {
		exclude(group: "net.fabricmc.fabric-api")
		exclude(group: "net.fabricmc", module: "fabric-loader")
	}

	modApi (libs.terraformwoodapi) {
		exclude(group: "net.fabricmc.fabric-api")
		exclude(group: "net.fabricmc", module: "fabric-loader")
	}
	include (libs.terraformwoodapi) {
		exclude(group: "net.fabricmc.fabric-api")
		exclude(group: "net.fabricmc", module: "fabric-loader")
	}
}

processResources {
	inputs.property "version", version

	filesMatching('fabric.mod.json') {
		expand "version": version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
	withJavadocJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		if (project.hasProperty("siphalorMavenUser")) {
			maven {
				name = "Siphalor"
				url = "https://maven.siphalor.de/upload.php"
				credentials {
					username = siphalorMavenUser
					password = siphalorMavenPassword
				}
			}
		}
	}
}
