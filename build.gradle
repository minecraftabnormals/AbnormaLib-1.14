plugins {
	id 'fabric-loom' version '0.9-SNAPSHOT'
	id 'maven-publish'
	id 'checkstyle'
	id 'org.cadixdev.licenser' version '0.6.1'
	id "co.riiid.gradle" version "0.4.2"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + "build." + project.build_number + "-" + project.minecraft_version
group = project.maven_group

minecraft {
	accessWidener = file("src/main/resources/vampirelib.accesswidener")
}

repositories {
	mavenCentral()
	mavenLocal()
	maven{ url="https://jitpack.io" }
	maven{ url="https://maven.fabricmc.net" }
	maven { url "https://raw.githubusercontent.com/vampire-studios/VampireStudiosMaven/main/" }
}

// Perform linting using Checkstyle
checkstyle {
	configFile file('.checkstyle.xml')
	toolVersion project.checkstyle_version
}

loom {
	customManifest = "https://gist.githubusercontent.com/modmuss50/6e00baf80dcbaa42f3a2fc846b290128/raw/f035ef8f75164f56ec6352809a34841d326bae1c/1_18_experimental-snapshot-1.json"
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation ("io.github.vampirestudios:artifice:0.15.10+21w20a") {
		exclude group: "net.fabricmc.fabric-api"
	}
	include ("io.github.vampirestudios:artifice:0.15.10+21w20a") {
		exclude group: "net.fabricmc.fabric-api"
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 16
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}
	repositories {
		maven {
			url file("E:\\Development\\Work\\VampireStudiosMaven\\")
		}
	}
}
