plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
	id("io.github.juuxel.loom-quiltflower-mini") version "1.2.0"
	id 'org.quiltmc.quilt-mappings-on-loom' version '3.1.1'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + "build." + project.build_number + "-" + project.minecraft_version
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/vampirelib.accesswidener")

	runs {
		datagen {
			server()
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"

			ideConfigGenerated = true
			runDir "build/datagen"
		}
		datagenClient {
			client()
			name "Data Generation Client"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"

			ideConfigGenerated = true
			runDir "build/datagen"
		}
	}
}

// Include resources generated by data generators.
sourceSets {
	main {
		resources {
			srcDirs += [
				'src/main/generated'
			]
		}
	}
}

repositories {
	mavenCentral()
	mavenLocal()
	maven { url = "https://maven.fabricmc.net" }
	maven { url = "https://raw.githubusercontent.com/vampire-studios/VampireStudiosMaven/main/" }
	maven { url = "https://maven.shedaniel.me/" }
	maven { url = uri("https://storage.googleapis.com/devan-maven/") }
	maven {
		name = "ParchmentMC"
		url = "https://maven.parchmentmc.net/"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered {
		it.addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:1.18.1-rc3+build.4:v2")) // https://maven.quiltmc.org/#browse/browse:release
		it.parchment("org.parchmentmc.data:parchment-1.18.1:BLEEDING-20211212.174449-1@zip") // https://ldtteam.jfrog.io/ui/native/parchmentmc-public/org/parchmentmc/data/parchment-1.18.1/BLEEDING-SNAPSHOT
		it.officialMojangMappings {
			setNameSyntheticMembers(false)
		}
	}

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modApi("me.shedaniel.cloth:cloth-config-fabric:6.0.42") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	include("me.shedaniel.cloth:cloth-config-fabric:6.0.42") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	modApi ("io.github.vampirestudios:artifice:0.15.10+21w20a") {
		exclude group: "net.fabricmc.fabric-api"
	}
	include ("io.github.vampirestudios:artifice:0.15.10+21w20a") {
		exclude group: "net.fabricmc.fabric-api"
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	withSourcesJar()
	withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			url file("D:\\Development\\Work\\VampireStudiosMaven\\")
		}
	}
}
