plugins {
	id 'fabric-loom' version '0.2.7-SNAPSHOT'
	id 'maven-publish'
	id 'net.minecrell.licenser' version '0.4.1'
	id "org.ajoberstar.grgit" version "3.1.1"
	id "co.riiid.gradle" version "0.4.2"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.wynprice.cursemaven" version "2.1.0"
}

def ENV = System.getenv()

def gittoken = System.getenv('GH_TOKEN')
def buildnumber = System.getenv('TRAVIS_BUILD_NUMBER')
def buildbranch = System.getenv('TRAVIS_BRANCH')

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

if (rootProject.file('private.gradle').exists()) {
	apply from: 'private.gradle'
}

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + "build." + project.build_number + "-" + project.minecraft_version
group = project.maven_group

minecraft {
	accessWidener = file("src/main/resources/vampirelib.accesswidener")
}

repositories {
	mavenCentral()
	mavenLocal()
	maven{ url="https://minecraft.curseforge.com/api/maven" }
	maven{ url="https://maven.fabricmc.net" }
	maven { url "https://server.bbkr.space/artifactory/libs-release" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"
	modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation("me.shedaniel:RoughlyEnoughItems:${project.rei_version}") {
		exclude group: "io.github.prospector.modmenu"
		exclude group :"net.fabricmc.fabric-api"
	}
	modImplementation "io.github.prospector:modmenu:${project.modmenu_version}"

	modImplementation "io.github.cottonmc:Jankson-Fabric:${project.jankson_version}"
	include "io.github.cottonmc:Jankson-Fabric:${project.jankson_version}"

	compile "com.google.code.findbugs:jsr305:3.0.2"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

configurations.all {
	resolutionStrategy {
		eachDependency {
			if (requested.group == "net.fabricmc") {
				if(requested.name =="tiny-mappings-parser"){
					useVersion("0.2.0.11")
				}

			}
		}
	}
}

github {
	owner = 'vampire-studios'
	repo = 'VampireLib-Fabric'
	token = gittoken ?: "a key"
	tagName = version + "-" + project.minecraft_version.replace(" ", "-")
	name = version + "-" + "MC" + project.minecraft_version.replace(" ", "-")
	targetCommitish = buildbranch
	prerelease = true
	assets = [
			"build/libs/${archivesBaseName}-${version}-dev.jar",
			"build/libs/${archivesBaseName}-${version}.jar",
			"build/libs/${archivesBaseName}-${version}-sources-dev.jar",
			"build/libs/${archivesBaseName}-${version}-sources.jar"
	]
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}