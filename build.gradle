plugins {
	id 'fabric-loom' version '0.5-SNAPSHOT'
	id 'maven-publish'
	id 'net.minecrell.licenser' version '0.4.1'
	id "co.riiid.gradle" version "0.4.2"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.wynprice.cursemaven" version "2.1.0"
}

def gittoken = System.getenv('GH_TOKEN')
def buildbranch = System.getenv('TRAVIS_BRANCH')

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

if (rootProject.file('private.gradle').exists()) {
	apply from: 'private.gradle'
}

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + "build." + project.build_number + "-" + project.minecraft_version
group = project.maven_group

minecraft {
	accessWidener = file("src/main/resources/vampirelib.accesswidener")
}

repositories {
	mavenCentral()
	mavenLocal()
	maven{ url="https://jitpack.io" }
	maven{ url="https://minecraft.curseforge.com/api/maven" }
	maven{ url="https://maven.fabricmc.net" }
	maven { url "https://server.bbkr.space/artifactory/libs-release" }
	maven {
		url = "https://maven.modmuss50.me/"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"
	modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modCompile ("me.shedaniel.cloth:config-2:${project.cloth_config_version}") { exclude group: "net.fabricmc.fabric-api" }
	include ("me.shedaniel.cloth:config-2:${project.cloth_config_version}") { exclude group: "net.fabricmc.fabric-api" }

	modCompile("me.sargunvohra.mcmods:autoconfig1u:${project.autoconfig1u}") {
		exclude module: "fabric-api"
	}
	include("me.sargunvohra.mcmods:autoconfig1u:${project.autoconfig1u}") {
		exclude module: "fabric-api"
	}

	modImplementation "com.github.cottonmc:Jankson-Fabric:1.16-SNAPSHOT"
	include "com.github.cottonmc:Jankson-Fabric:1.16-SNAPSHOT"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

github {
	owner = 'vampire-studios'
	repo = 'VampireLib-Fabric'
	token = gittoken ?: "a key"
	tagName = version + "-" + project.minecraft_version.replace(" ", "-")
	name = version + "-" + "MC" + project.minecraft_version.replace(" ", "-")
	targetCommitish = buildbranch
	prerelease = true
	assets = [
			"build/libs/${archivesBaseName}-${version}.jar",
			"build/libs/${archivesBaseName}-${version}-sources.jar"
	]
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}
	repositories {}
}